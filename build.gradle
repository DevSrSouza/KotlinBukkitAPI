plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.2.31'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id 'net.minecrell.plugin-yml.bukkit' version '0.2.1'
}

wrapper {
    gradleVersion = '4.2'
}

group 'br.com.devsrsouza'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {

    jcenter()
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    // PLUGINS REPO
    maven { url 'https://jitpack.io' }

    maven {
        name = "vault-repo"
        url = "http://nexus.hc.to/content/repositories/pub_releases"
    }
    maven {
        name = 'placeholderapi'
        url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "mvdw-software"
        url = 'http://repo.mvdw-software.be/content/groups/public/'
    }
    maven {
        name = 'inventive-repo'
        url = 'https://repo.inventivetalent.org/content/groups/public/'
    }
}

sourceSets {
    main.java.srcDirs += 'text/src/main/java'
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile("org.jetbrains.kotlin:kotlin-reflect")

    compileOnly 'org.bukkit:bukkit:1.12-R0.1-SNAPSHOT'

    // PLUGINS
    compileOnly 'net.milkbowl.vault:VaultAPI:1.6'
    compileOnly 'me.clip:placeholderapi:2.8.5'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:2.5.2-SNAPSHOT') {
        exclude group: 'org.spigotmc', module: 'spigot'
    }

    compileOnly 'com.github.connorlinfoot:ActionBarAPI:master-SNAPSHOT'
    compileOnly 'com.github.connorlinfoot:TitleAPI:master-SNAPSHOT'

    compileOnly 'org.inventivetalent.packetlistener:api:3.6.0-SNAPSHOT'
    compileOnly('org.inventivetalent:hologramapi:1.6.0')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    baseName = 'KotlinBukkitAPI'
    classifier = null
}

bukkit {

    name = 'KotlinBukkitAPI'
    version = project.version
    main = 'br.com.devsrsouza.kotlinbukkitapi.KotlinBukkitAPI'
    softDepend = ['Vault', 'PlaceholderAPI', 'MVdWPlaceholderAPI']

    website = 'https://github.com/DevSrSouza/KotlinBukkitAPI'
    authors = ['DevSrSouza']

}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://www.gnu.org/licenses/gpl.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "DevSrSouza"
            name "Gabriel Souza"
            email "devsrsouza@gmail.com"
        }
    }

    scm {
        url "https://github.com/DevSrSouza/KotlinBukkitAPI/tree/master"
    }
}

publishing {
    publications {
        MavenBuild(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'kotlinbukkitapi'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'KotlinBukkitAPI is a API for Bukkit using the cool features of Kotlin to make your lifes much easely.')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/DevSrSouza/KotlinBukkitAPI')
                root.remove(root.get("dependencies"))
                root.children().last() + pomConfig
            }
        }
    }
}